// Package and import specifications section.

package compiler488.parser;

import java_cup.runtime.*;		// Must always import this.
import compiler488.ast.*;		// import all the AST classes (might be a bit overkill)
import compiler488.ast.decl.*;
import compiler488.ast.expn.*;
import compiler488.ast.stmt.*;
import compiler488.ast.type.*;

/* User code components 1: action code components.
 * Code to be used inside the actions triggered when productions are matched.
 * If any common code is needed for more than one action , put it here.
 */
// action code {:
// your code goes HERE
//:};

/* User code components 2: parser code components. 
 * Modifications to the default generated parser
 */
parser code {:

	String lastError;
	
	/* Override the report_error method so it will display the line and
	 * column of where the error occurred in the input as well as the
	 * reason for the error which is passed into the method in the
	 * String 'message'.
	 * @param message  error message to print
	 * @param info	 symbol containing line/column numbers
	 */
	public void report_error(String message, Object info)
	{
	String st =  "Error";
   
		if (info instanceof java_cup.runtime.Symbol)
		{
			java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);

		/* s.left is supposed to hold the line number of the error.
		 * s.right is supposed to hold the column number.
			 * If either is < 0 the parser may have run off the end of the program
			 * and a Syntax Error message without line number may be printed.
		 */
   
			if (s.left >= 0)				// Check line number.
			{
				st += " in line " + (s.left + 1);
				if (s.right >= 0)			// Check column number.
					st += ", column " + (s.right + 1);
			}
			else
			   st += " at end of input " ;
		}
		st += ": " + message;
		System.err.println (st);
		lastError = st;
	}
   
	// Override the report_fatal_error method to use the report_error method.
	/** @throws SyntaxErrorException						*/
	public void report_fatal_error (String message, Object info) throws SyntaxErrorException
	{
		report_error (message, info);
		throw new SyntaxErrorException (lastError);
	}
   
	// version string for printing version in Main
	public static final String version = "Winter 2017" ;

:};

/* User code components 3: initialization code.
 * If you want some code to be executed before the parser asks for the first
 * token, add it here.
 */
init with {:
:};

/* User code components 4: scanner specification code.
 * If you need to override the way the parser asks the scanner for the
 * next token, put your preferred method here.  Uncomment the line following
 * this comment, replace the line after that with your code, and uncomment
 * the line after that.
 */
// scan with {:
// your code goes here
// :};

/* Symbol lists section.
 * The names of terminals and non-terminals are not allowed to be CUP
 * reserved words.  These include "code", "action", "parser", "terminal",
 * "non", "nonterminal", "init", "scan", "with", "start", "precedence",
 * "left", "right", "nonassoc", "import", and "package".
 */

// DEFINITIONS for CSC488S Source Language  Winter  2017 

// Terminals returned by the scanner with no value attached.  
terminal	AND,	OR,	NOT,	TRUE,	FALSE	;
terminal 	BOOLEAN,INTEGER,FUNCTION,PROCEDURE	;
terminal	DO,	ELSE,	EXIT,   IF	;
terminal	READ, 	REPEAT,	RETURN,	NEWLINE,THEN,	UNTIL	;
terminal	VAR,	WHEN,	WHILE,	WITHX,	WRITE 	;

// Special-character terminals, no value attached
terminal	L_PAREN	,R_PAREN,L_SQUARE, R_SQUARE, L_CURLEY, R_CURLEY	;
terminal	EQUAL,	GREATER,LESS,	COMMA,  DOT, QUESTION, COLON	;
terminal	PLUS,	MINUS,	TIMES,  DIVIDE	;

// Terminals returned by the scanner with a value.
terminal String  IDENT	;
terminal Integer INTCONST	;
terminal String  TEXTCONST	;

// Non-terminals that have no value.
// nonterminal xxx

nonterminal	program,	statement,	declaration,	scope	;
nonterminal	statements,	declarations,	matchedStatement, unmatchedStatement ;
nonterminal	variablenames,		output			;
nonterminal	type,  	arguments,	parameters,	variable	;
nonterminal 	expression,	variablename 			;

nonterminal	inputs, outputs, bound ;
nonterminal	procedureCall, parameter, conjunction, negation, compareExpn ;
nonterminal	arithExpn, term, factor, primary, constant ;

// Non-terminals that have a value.
// nonterminal type  nameList ;

// PLEASE NOTE: your grammar can not use the 'precedence'
// feature of java-cup. The lines below should remain
// commented out.
//
//precedence left PLUS, MINUS;
//precedence left TIMES, DIVIDE;		// Higher precedence.
precedence   left ELSE ;

// The grammar section.

// This specifies the initial symbol of the grammar.
start with program;

/* Notice that sometimes the action must be inside the production rule,
 * not after it.  This is a subtle yet significant difference from YACC.
 * This is because the CUP parser is looking ahead one token.  It ALWAYS
 * reads one more token before it will act on the production.
 */

program		::=	scope:s	
				{: 
				Scope scope = (Scope) s;
				Program program = new Program();
				program.setDeclarations(scope.getDeclarations());
				program.setStatements(scope.getStatements());
				RESULT = program;
				:}
				;								// the main program

scope		::=	L_CURLEY  declarations:d statements:s  R_CURLEY  	// scope with declarations and statements
			{: 
			// System.out.println("Found " + d);
			// System.out.println("Found " + s);
			Scope scope = new Scope();
			scope.setStatements((ASTList<Stmt>)s);
			scope.setDeclarations((ASTList<Declaration>)d);
			RESULT = scope;
			:}
		|	L_CURLEY statements:s  R_CURLEY 		// scope with statments only
			{: 
			Scope scope = new Scope();
			scope.setStatements((ASTList<Stmt>)s);
			RESULT = scope;
			:}
		|	L_CURLEY  R_CURLEY			// empty scope
			{: 
			Scope scope = new Scope();
			RESULT = scope;
			:}
		;

statements	::=	statement:s						// sequence of statements
			{: 
			ASTList<Stmt> statement_list = new ASTList<Stmt>((Stmt)s);
			RESULT = statement_list;
			:}
		|	statements:ss statement:s
			{: 
			RESULT = ((ASTList<Stmt>)ss).addLast((Stmt)s);
			:}
		;

declarations	::=	declaration:d						// sequence of declarations
			{: 
				ASTList<Declaration> declaration_list = new ASTList<Declaration>((Declaration) d);
				RESULT = declaration_list;
			:}
		|	declarations:ds declaration:d
			{: 
				ASTList<Declaration> declaration_list = (ASTList<Declaration>) ds;
				RESULT = declaration_list.addLast((Declaration) d);
			:}
		;

statement	::=	matchedStatement:m
			{: 
			RESULT = m;
			:}
		|	unmatchedStatement:u
			{: 
			RESULT = u;
			:}
		;

matchedStatement::=	variable:v COLON EQUAL expression:e				// assignment statement
			{: 
			AssignStmt lst = new AssignStmt();
			lst.setLval((Expn)v);
			lst.setRval((Expn)e);
			RESULT = lst; 
			:}
		|	IF expression:e THEN matchedStatement:m1 ELSE matchedStatement:m2 // if statements
			{:
				IfStmt stmt = new IfStmt();
				stmt.setCondition((Expn) e);
				stmt.setWhenTrue((Stmt) m1);
				stmt.setWhenFalse((Stmt) m2);
				RESULT = stmt;
			:}
		|	WHILE expression:e DO statement:s				// while statement
			{:
				WhileDoStmt stmt = new WhileDoStmt();
				stmt.setExpn((Expn) e);
				stmt.setBody((Stmt) s);
				RESULT = stmt;
			:}
		|	REPEAT statement:s  UNTIL expression:e			// repeat statement
			{:
				RepeatUntilStmt stmt = new RepeatUntilStmt();
				stmt.setExpn((Expn) e);
				stmt.setBody((Stmt) s);
				RESULT = stmt;
			:}
		|	EXIT							// exit statements
			{:
				ExitStmt stmt = new ExitStmt();
				RESULT = stmt;
			:}
		|	EXIT INTCONST:i
			{:
				ExitStmt stmt = new ExitStmt();
				stmt.setLevel(i);
				RESULT = stmt;
			:}
		|	EXIT WHEN expression:e
			{:
				ExitStmt stmt = new ExitStmt();
				stmt.setExpn((Expn) e);
				RESULT = stmt;
			:}
		|	EXIT INTCONST:i WHEN expression:e
			{:
				ExitStmt stmt = new ExitStmt();
				stmt.setExpn((Expn) e);
				stmt.setLevel(i);
				RESULT = stmt;
			:}
		|	RETURN WITHX  expression:e 
			{:
				ReturnStmt stmt = new ReturnStmt();
				stmt.setValue((Expn) e);
				RESULT = stmt;
			:}
		|	RETURN 							// return statements
			{:
				RESULT = new ReturnStmt();
			:}
		|	WRITE outputs:o						// input/output statments
			{: 
				ASTList<Printable> lst = (ASTList<Printable>)o;
				WriteStmt stmt = new WriteStmt();
				stmt.setOutputs(lst);
				RESULT = stmt; 
			:}
		|	READ  inputs:i
			{: 
				ReadStmt stmt = new ReadStmt();
				stmt.setInputs((ASTList<compiler488.ast.Readable>) i);
				RESULT = stmt; 
			:}
		|	procedureCall:p						// procedure call
			{: 
				RESULT = p; 
			:}
		|	scope:s							// embedded scope
			{: 
				RESULT = s; 
			:}
		;

unmatchedStatement::=	IF expression:e THEN statement:s	
			{:
				IfStmt stmt = new IfStmt();
				stmt.setCondition((Expn) e);
				stmt.setWhenTrue((Stmt) s);
				RESULT = stmt;
			:}
		|	IF expression:e THEN matchedStatement:m ELSE unmatchedStatement:u
			{:
				IfStmt stmt = new IfStmt();
				stmt.setCondition((Expn) e);
				stmt.setWhenTrue((Stmt) m);
				stmt.setWhenFalse((Stmt) u);
				RESULT = stmt;
			:}
		;

procedureCall	::=	IDENT:i 							// call procedure with no parameters
			{:
				ProcedureCallStmt stmt = new ProcedureCallStmt();
				stmt.setName(i);
				RESULT = stmt;
			:}
		|	IDENT:i L_PAREN arguments:a R_PAREN  			// call procedure with parameters
			{:
				ProcedureCallStmt stmt = new ProcedureCallStmt();
				stmt.setName(i);
				stmt.setArguments((ASTList<Expn>) a);
				RESULT = stmt;
			:}
		;

declaration	::=	VAR variablenames:vs COLON type:t			// declare scalar/array variables
			{:
				MultiDeclarations decl = new MultiDeclarations();
				decl.setElements((ASTList<DeclarationPart>) vs);
				decl.setType((Type) t);
				RESULT = decl;
			:}
		|	FUNCTION IDENT:i COLON type:t scope:s			// declare function
			{:
				RoutineDecl decl = new RoutineDecl();
				RoutineBody body = new RoutineBody();
				body.setBody((Scope) s);
				decl.setName(i);
				decl.setType((Type) t);
				decl.setRoutineBody(body);
				RESULT = decl;
			:}
		|	FUNCTION  IDENT:i L_PAREN parameters:p R_PAREN COLON type:t scope:s
			{:
				RoutineDecl decl = new RoutineDecl();
				RoutineBody body = new RoutineBody();
				body.setBody((Scope) s);
				body.setParameters((ASTList<ScalarDecl>) p);
				decl.setName(i);
				decl.setType((Type) t);
				decl.setRoutineBody(body);
				RESULT = decl;
			:}
		|	PROCEDURE IDENT:i scope:s			//  declare procedure
			{:
				RoutineDecl decl = new RoutineDecl();
				RoutineBody body = new RoutineBody();
				body.setBody((Scope) s);
				decl.setName(i);
				decl.setRoutineBody(body);
				RESULT = decl;
			:}
		|	PROCEDURE IDENT:i L_PAREN parameters:p R_PAREN scope:s
			{:
				RoutineDecl decl = new RoutineDecl();
				RoutineBody body = new RoutineBody();
				body.setBody((Scope) s);
				body.setParameters((ASTList<ScalarDecl>) p);
				decl.setName(i);
				decl.setRoutineBody(body);
				RESULT = decl;
			:}
		;

variablenames	::=	variablename:v  				// list of variables being declared
			{:
				RESULT = new ASTList<DeclarationPart>((DeclarationPart) v);
			:}
		|	variablenames:vs COMMA variablename:v 
			{:
				ASTList<DeclarationPart> lst = (ASTList<DeclarationPart>) vs;
				RESULT = lst.addLast((DeclarationPart)v);
			:}
		;

variablename	::=	IDENT:i 						// scalar variable
			{:
				ScalarDeclPart expn = new ScalarDeclPart();
				expn.setName(i);
				RESULT = expn;
			:}
		|	IDENT:i1 L_SQUARE INTCONST:i2 R_SQUARE		// array variable with upper bound
			{:
				ArrayDeclPart decl = new ArrayDeclPart();
				decl.setName(i1);
				decl.setLowerBoundary(1);		// If a lower bound isn't given, then its set to 1 by default
				decl.setUpperBoundary((Integer) i2);
				RESULT = decl;
			:}
		|	IDENT:i1 L_SQUARE bound:i2 DOT DOT bound:i3  R_SQUARE	// array variable with upper/lower bounds
			{:
				ArrayDeclPart decl = new ArrayDeclPart();
				decl.setName(i1);
				decl.setLowerBoundary((Integer) i2);		// If a lower bound isn't given, then its set to 1 by default
				decl.setUpperBoundary((Integer) i3);
				RESULT = decl;
			:}
		;

bound		::=	INTCONST:i					// positive array bound
			{:
				RESULT = i;
			:}
		|	MINUS INTCONST:i					// negative array bound
			{:
				RESULT = -1 * i;
			:}
		;

type		::=	INTEGER			// integer type
			{:
				RESULT = new IntegerType();
			:}
		|	BOOLEAN			// boolean type
			{:
				RESULT = new BooleanType();
			:}
		;

outputs		::=	output:o			// list of output expressions
			{:
				ASTList<Printable> lst = new ASTList<Printable>((Printable)o);
				RESULT = lst;
			:}
		|	outputs:os COMMA output:o
			{:
				ASTList<Printable> lst = (ASTList<Printable>) os;
				RESULT = lst.addLast((Printable)o);
			:}
		;

output		::=	expression:e		// integer expression
			{:
				RESULT = e;
			:}
		|	TEXTCONST:t		// text constant
			{: 
				TextConstExpn str = new TextConstExpn();
				str.setValue(t);
				RESULT = str;
			:}
		|	NEWLINE			// newline constant
			{:
				SkipConstExpn expn = new SkipConstExpn();
				RESULT = expn;
			:}
		;

inputs		::=	variable:v		// list of variables being read into
			{:
				ASTList<compiler488.ast.Readable> lst = new ASTList<compiler488.ast.Readable>((compiler488.ast.Readable)v);
				RESULT = lst;
			:}
		|	inputs:i COMMA variable:v
			{:
				ASTList<compiler488.ast.Readable> params = (ASTList<compiler488.ast.Readable>) i;
				RESULT = params.addLast((compiler488.ast.Readable)v);
			:}
		;

arguments	::=	expression:e			// list of function/procedure arguments
			{:
				ASTList<Expn> lst = new ASTList<Expn>((Expn)e);
				RESULT = lst;
			:}
		|	arguments:a COMMA expression:e
			{:
				ASTList<Expn> params = (ASTList<Expn>) a;
				RESULT = params.addLast((Expn)e);
			:}
		;

parameters	::=	parameter:p			// list of function/procedure formal parameters
			{:
				ASTList<ScalarDecl> lst = new ASTList<ScalarDecl>((ScalarDecl)p);
				RESULT = lst;
			:}
		|	parameters:ps COMMA parameter:p
			{:
				ASTList<ScalarDecl> params = (ASTList<ScalarDecl>) ps;
				RESULT = params.addLast((ScalarDecl)p);
			:}
		;

parameter	::=	IDENT:i COLON type:t 		// declare one parameter
			{:
				ScalarDecl decl = new ScalarDecl();
				decl.setName(i);
				decl.setType((Type)t);
				RESULT = decl;
			:}
		;

expression	::=	conjunction:c			// disjunctions, associate left-to-right
			{:
				RESULT = c;
			:}
		|	expression:e OR conjunction:c
			{:
				BoolExpn expn = new BoolExpn();
				expn.setLeft((Expn)e);
				expn.setOpSymbol("OR");
				expn.setRight((Expn)c);
				RESULT = expn;
			:}
		;

conjunction	::=	negation:n			// conjunctions, associate left-to-right
			{:
				RESULT = n;
			:}
		|	conjunction:c AND negation:n
			{:
				BoolExpn expn = new BoolExpn();
				expn.setLeft((Expn)c);
				expn.setOpSymbol("AND");
				expn.setRight((Expn)n);
				RESULT = expn;
			:}
		;

negation	::=	compareExpn:e 			// negations
			{:
				RESULT = e;
			:}
		|	NOT negation:n
			{:
				NotExpn expn = new NotExpn();
				expn.setOpSymbol("NOT");
				expn.setOperand((Expn)n);
				RESULT = expn;
			:}
		;

compareExpn	::=	arithExpn:a			// comparisons, do not associate
			{:
				RESULT = a;
			:}
		|	arithExpn:a1 EQUAL arithExpn:a2
			{:
				CompareExpn expn = new CompareExpn();
				expn.setLeft((Expn)a1);
				expn.setOpSymbol("EQUAL");
				expn.setRight((Expn)a2);
				RESULT = expn;
			:}
		| 	arithExpn:a1 NOT EQUAL arithExpn:a2
			{:
				CompareExpn expn = new CompareExpn();
				expn.setLeft((Expn)a1);
				expn.setOpSymbol("NOT EQUAL");
				expn.setRight((Expn)a2);
				RESULT = expn;
			:}
		|	arithExpn:a1 LESS arithExpn:a2
			{:
				CompareExpn expn = new CompareExpn();
				expn.setLeft((Expn)a1);
				expn.setOpSymbol("LESS");
				expn.setRight((Expn)a2);
				RESULT = expn;
			:}
		|	arithExpn:a1 LESS EQUAL arithExpn:a2
			{:
				CompareExpn expn = new CompareExpn();
				expn.setLeft((Expn)a1);
				expn.setOpSymbol("LESS EQUAL");
				expn.setRight((Expn)a2);
				RESULT = expn;
			:}
		|	arithExpn:a1 GREATER arithExpn:a2
			{:
				CompareExpn expn = new CompareExpn();
				expn.setLeft((Expn)a1);
				expn.setOpSymbol("GREATER");
				expn.setRight((Expn)a2);
				RESULT = expn;
			:}
		|	arithExpn:a1 GREATER EQUAL arithExpn:a2
			{:
				CompareExpn expn = new CompareExpn();
				expn.setLeft((Expn)a1);
				expn.setOpSymbol("GREATER EQUAL");
				expn.setRight((Expn)a2);
				RESULT = expn;
			:}
		;

arithExpn	::=	term:t				// expressions, associate + , - left-to-right
			{:
				RESULT = t;
			:}
		|	arithExpn:a  PLUS  term:t
			{:
				ArithExpn expn = new ArithExpn();
				expn.setLeft((Expn)a);
				expn.setOpSymbol("PLUS");
				expn.setRight((Expn)t);
				RESULT = expn;
			:}
		|	arithExpn:a  MINUS term:t
			{:
				ArithExpn expn = new ArithExpn();
				expn.setLeft((Expn)a);
				expn.setOpSymbol("MINUS");
				expn.setRight((Expn)t);
				RESULT = expn;
			:}
		;

term		::=	factor:f				// terms, associate * , /   left-to-right
			{:
				RESULT = f;
			:}
		|	term:t TIMES factor:f
			{:
				ArithExpn expn = new ArithExpn();
				expn.setLeft((Expn)t);
				expn.setOpSymbol("TIMES");
				expn.setRight((Expn)f);
				RESULT = expn;
			:}
		|	term:t DIVIDE factor:f
			{:
				ArithExpn expn = new ArithExpn();
				expn.setLeft((Expn)t);
				expn.setOpSymbol("DIVIDE");
				expn.setRight((Expn)f);
				RESULT = expn;
			:}
		;

factor		::=	primary:p				// unary minus
			{:
				RESULT = p;
			:}
		|	MINUS factor:f
			{:
				UnaryMinusExpn expn = new UnaryMinusExpn();
				expn.setOperand((Expn)f);
				expn.setOpSymbol("MINUS");
				RESULT = expn;
			:}
		;

primary		::=	constant:c				// integer or boolean constant
			{:
				RESULT = c;
			:}
		|	variable:v				// scalar, array variable or function call
			{:
				RESULT = v;
			:}
		|	IDENT:i L_PAREN arguments:a R_PAREN 	// function call
			{:
				FunctionCallExpn expn = new FunctionCallExpn();
				expn.setIdent(i);
				expn.setArguments((ASTList<Expn>) a);
				RESULT = expn;
			:}
		| 	L_PAREN  expression:e  R_PAREN		// parenthesized expression
			{:
				RESULT = e;
			:}
		|	L_PAREN  expression:e1 QUESTION expression:e2 COLON expression:e3 R_PAREN
			{:
				ConditionalExpn expn = new ConditionalExpn();
				expn.setCondition((Expn)e1);
				expn.setTrueValue((Expn)e2);
				expn.setFalseValue((Expn)e3);
				RESULT = expn;
			:}
		;

constant	::=	INTCONST:i	// integer constant
			{:
				IntConstExpn expn = new IntConstExpn();
				expn.setValue(i);
				RESULT = expn;
			:}
		|	TRUE		// constant true
			{:
				BoolConstExpn expn = new BoolConstExpn();
				expn.setValue(true);
				RESULT = expn;
			:}
		|	FALSE		// constant false
			{:
				BoolConstExpn expn = new BoolConstExpn();
				expn.setValue(false);
				RESULT = expn;
			:}
		;

variable	::=	IDENT:i					// scalar variable or function call
			{:
				IdentExpn expn = new IdentExpn();
				expn.setIdent(i);
				RESULT = expn;
			:}
		|	IDENT:i L_SQUARE expression:e R_SQUARE	// element of an array
			{:
				SubsExpn expn = new SubsExpn();
				expn.setOpSymbol("SUBS");
				expn.setOperand((Expn)e);
				expn.setVariable((String)i);
				RESULT = expn;
			:}
		;


